plugins {
    id 'java-library'
    id 'org.gradlex.extra-java-module-info' version '1.9'
}

group = 'com.sparrowwallet'
version = '0.9'

repositories {
    mavenCentral()
    maven { url 'https://code.sparrowwallet.com/api/packages/sparrowwallet/maven' }
}

tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

java {
    withSourcesJar()
}

dependencies {
    implementation(project(':drongo'))
    implementation('org.hid4java:hid4java:0.8.0')
    implementation('com.fazecast:jSerialComm:2.11.0')
    implementation('org.usb4java:usb4java:1.3.0')
    implementation('co.nstant.in:cbor:0.9')
    implementation('com.fasterxml.jackson.core:jackson-databind:2.17.2')
    implementation('org.apache.commons:commons-lang3:3.7')
    implementation('com.google.guava:guava:33.0.0-jre')
    implementation('com.google.protobuf:protobuf-java:4.28.3')
    implementation('commons-codec:commons-codec:1.17.1')
    implementation('org.slf4j:slf4j-api:2.0.12')
    implementation('com.sparrowwallet:tern:1.0.3')
    implementation('io.reactivex.rxjava2:rxjava:2.2.15')
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation('org.junit.jupiter:junit-jupiter')
}

test {
    useJUnitPlatform()
}

extraJavaModuleInfo {
    module('co.nstant.in:cbor', 'co.nstant.in.cbor') {
        exports('co.nstant.in.cbor')
        exports('co.nstant.in.cbor.model')
        exports('co.nstant.in.cbor.builder')
    }
    module('org.hid4java:hid4java', 'org.hid4java') {
        exports('org.hid4java')
    }
    module('org.usb4java:usb4java', 'org.usb4java') {
        exports('org.usb4java')
    }
    module('org.usb4java:libusb4java|linux-x86', 'org.usb4java.linux_x86')
    module('org.usb4java:libusb4java|linux-x86-64', 'org.usb4java.linux_x86_64')
    module('org.usb4java:libusb4java|win32-x86', 'org.usb4java.win32_x86')
    module('org.usb4java:libusb4java|win32-x86-64', 'org.usb4java.win32_x86_64')
    module('org.usb4java:libusb4java|darwin-x86-64', 'org.usb4java.darwin_x86_64')
    module('org.usb4java:libusb4java|linux-arm', 'org.usb4java.linux_arm')
    module('org.usb4java:libusb4java|linux-aarch64', 'org.usb4java.linux_aarch64')
    module('com.google.guava:listenablefuture|empty-to-avoid-conflict-with-guava', 'com.google.guava.listenablefuture')
    module('com.google.code.findbugs:jsr305', 'com.google.code.findbugs.jsr305')
    module('j2objc-annotations-2.8.jar', 'com.google.j2objc.j2objc.annotations', '2.8')
}